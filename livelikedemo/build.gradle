apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.livelike.livelikedemo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        multiDexEnabled true
        versionCode 1
        versionName "1.1"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {}
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions 'default'
    productFlavors {
        staging {
            dimension 'default'
            buildConfigField "String", "APP_CLIENT_ID", '"vLgjH7dF0uX4J4FQJK3ncMkVmsCdLWhJ0qPtsbk7"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast-staging.livelikecdn.com/api/v1/programs/?client_id=vLgjH7dF0uX4J4FQJK3ncMkVmsCdLWhJ0qPtsbk7"'
            applicationIdSuffix ".debug"
        }
        production {
            dimension 'default'
            buildConfigField "String", "APP_CLIENT_ID", '"8PqSNDgIVHnXuJuGte1HdvOjOqhCFE1ZCR3qhqaS"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast.livelikecdn.com/api/v1/programs/?client_id=8PqSNDgIVHnXuJuGte1HdvOjOqhCFE1ZCR3qhqaS"'
        }
        qatesting {
            dimension 'default'
            applicationIdSuffix ".qa"
            buildConfigField "String", "APP_CLIENT_ID", '"pnODbVXg0UI80s0l2aH5Y7FOuGbftoAdSNqpdvo6"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast-qa.livelikecdn.com/api/v1/programs/?client_id=pnODbVXg0UI80s0l2aH5Y7FOuGbftoAdSNqpdvo6"'
        }
        qaiconictesting {
            dimension 'default'
            applicationIdSuffix ".iconicqa"
            buildConfigField "String", "APP_CLIENT_ID", '"LT9lUmrzSqXvAL66rSWSGK0weclpFNbHANUTxW9O"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast-iconic.livelikecdn.com/api/v1/programs/?client_id=LT9lUmrzSqXvAL66rSWSGK0weclpFNbHANUTxW9O"'
        }
        qagamechangerstesting {
            dimension 'default'
            applicationIdSuffix ".gamechangersqa"
            buildConfigField "String", "APP_CLIENT_ID", '"GaEBcpVrCxiJOSNu4bvX6krEaguxHR9Hlp63tK6L"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast-game-changers.livelikecdn.com/api/v1/programs/?client_id=GaEBcpVrCxiJOSNu4bvX6krEaguxHR9Hlp63tK6L"'
        }
        qadigtesting {
            dimension 'default'
            applicationIdSuffix ".digqa"
            buildConfigField "String", "APP_CLIENT_ID", '"lom9db0XtQUhOZQq1vz8QPfSpiyyxppiUVGMcAje"'
            buildConfigField "String", "TEST_CONFIG_URL", '"https://cf-blast-dig.livelikecdn.com/api/v1/programs/?client_id=lom9db0XtQUhOZQq1vz8QPfSpiyyxppiUVGMcAje"'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation 'androidx.multidex:multidex:2.0.1'

    // Tests frameworks
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    //players
    implementation "com.google.android.exoplayer:exoplayer:$exoplayerVersion"
    implementation 'androidx.media:media:1.3.1'

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    //gson
    implementation "com.google.code.gson:gson:$gsonVersion"
    // Time lib
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"

    //sdk
    debugApi project(path: ':engagementsdk')
    releaseApi project(path: ':engagementsdk')
    //TODO  define configuration: 'release' for release variant currently it is not building in it.

    implementation project(path: ':pluginexoplayer')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"
    implementation "com.airbnb.android:lottie:$lottieVersion"
    implementation 'com.github.anastr:speedviewlib:1.5.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}

def reportsDirectory = "$buildDir/reports/androidTests/connected"

def clearScreenshotsTask = task('clearScreenshots', type: Exec) {
    executable "${android.getAdbExe().toString()}"
    args 'shell', 'rm', '-r', '/sdcard/Pictures/my_app_folder'
}

def createScreenshotDirectoryTask = task('createScreenshotDirectory', type: Exec, group: 'reporting') {
    executable "${android.getAdbExe().toString()}"
    args 'shell', 'mkdir', '-p', '/sdcard/Pictures/my_app_folder'
}

def fetchScreenshotsTask = task('fetchScreenshots', type: Exec, group: 'reporting') {
    executable "${android.getAdbExe().toString()}"
    args 'pull', '/sdcard/Pictures/my_app_folder/.', reportsDirectory
    finalizedBy {
        clearScreenshotsTask
    }

    dependsOn {
        createScreenshotDirectoryTask
    }

    doFirst {
        new File(reportsDirectory).mkdirs()
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'connectedDebugAndroidTest') {
        task.finalizedBy {
            fetchScreenshotsTask
        }
    }
}
